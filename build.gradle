/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at:
 *
 *     http://aws.amazon.com/apache2.0/
 *
 * or in the "license" file accompanying this file. This file is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 */

buildscript {
    ext.ionVersion = "[1.2,)"
    ext.kotlinVersion = "[1.2,1.9]"
    ext.junitVersion = "[5.3,)"

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "org.jetbrains.kotlin.jvm" version "1.9.0"

    // benchmark
    id "me.champeau.jmh" version "0.7.2"

    // Deployment
    id "maven-publish"
    id "signing"
}

// Dependencies --------------------------------------------------------------

repositories {
    mavenCentral()
}

group 'com.amazon.ion'
archivesBaseName = 'ion-java-path-extraction'
version '1.5.0'

sourceCompatibility = 1.8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "com.amazon.ion:ion-java:$ionVersion"

    // using kotlin to make tests less verbose
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

    // JUnit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}


test {
    useJUnitPlatform()
}

jmh {
    fork = 1
    benchmarkMode = ["thrpt"]
    failOnError = true

    // warmup
    warmupIterations = 2

    // iterations
    iterations = 5
}

checkstyle {
    toolVersion = "8.37"
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
    configDirectory = file("$rootProject.projectDir/config/checkstyle")
}
tasks.withType(Checkstyle) {
    reports {
        html.required = true
    }
}

// Publishing
// -------------------------------------------------------------------------------

task sourcesJar(type: Jar) {
    from "src"
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "ion-java-path-extraction"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Ion Java Path Extraction"
                packaging = "jar"
                url = "https://github.com/amzn/ion-java-path-extraction"
                description = "Ion Path Extraction API aims to combine the convenience of a DOM API with the speed of a streaming API."
                scm {
                    connection = "scm:git@github.com:amzn/ion-java-path-extraction.git"
                    developerConnection = "scm:git@github.com:amzn/ion-java-path-extraction.git"
                    url = "git@github.com:amzn/ion-java-path-extraction.git"
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "ion-team@amazon.com"
                        organization = "Amazon Labs"
                        organizationUrl = "https://github.com/amzn"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
